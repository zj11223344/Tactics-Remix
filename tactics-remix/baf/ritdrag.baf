/////////////////////////////////////////////////////////////////////
// Ira'kiou'dneg, 23th level Dragon Mage
// 
// Basic strategy is taken from Sion in the Improved Guarded Compound,
// by way of Shangalar from the Improved Twisted Rune. TOB only!
// 
// 6 feats!
// 1 = Extra 6th
// 1 = Extra 7th
// 1 = Extra 8th
// 1 = whatever ...
// 
// We completely ignore in-game memorization (hey, who doesn't?) and
// use global variables to keep track of spells. 
//
// Memorized Spells By Level: 5 5 5 5 5 5+1 5+1 3+1 3 
// Memorized Spells (number, name, casting time): 
//	5 magic missile (1)
//
//	5 melf acid (2)          
//
//	1 remove magic (3)
//	4 slow (3)
//
//	1 greater malison (4)
//	4 stoneskin (1) 
//
//	4 chaos (4)
//      1 immunity
//
//	5 pro magic weapons (1)
//
//	5 mordy (7)
//
//	4 wilting (8)
//
//	3 time stop (9)
//
//      Already Cast: 
//              Chain Contingency: spell trap, immunity abj, time stop
//              Spell Trigger: imp-haste, pro-mw, pro-me
//              Contingency: pro-mw
//              sequencer: imp-invis, etc.
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Battle Prep
/////////////////////////////////////////////////////////////////////
IF
	Global("Prep","LOCALS",0)
        See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("Prep","LOCALS",1)
                SetGlobal("ChainContingency","LOCALS",0)
                SetGlobal("Contingency","LOCALS",0)
                SetGlobal("Sequencer","LOCALS",0)

		SetGlobalTimer("Cast","LOCALS",0)
		SetGlobalTimer("TimeStop","LOCALS",0)
  
                SetGlobalTimer("YELLOWDRAGONBREATH","GLOBAL",0)

                DisplayString(Myself,39968) // Spell Trigger - Fired
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		ReallyForceSpell(Myself,WIZARD_IMPROVED_HASTE)

		SetGlobalTimer("RecastProMagWeap","LOCALS",24)
                GiveItemCreate("potn55",Myself,5,0,0)   // super healing

		// Re-memorize
		SetGlobal("Time","LOCALS",3)

		SetGlobal("Wilt","LOCALS",4)

		SetGlobal("Mordy","LOCALS",5)

                SetGlobal("ProMagWeap","LOCALS",5)

                SetGlobal("Chaos","LOCALS",4)
                SetGlobal("Abjure","LOCALS",0) // in chain contingency
                SetGlobal("SpellShield","LOCALS",1)

                SetGlobal("Stoneskin","LOCALS",4)
                SetGlobal("Malison","LOCALS",1)

                SetGlobal("Remove","LOCALS",0)
                SetGlobal("Slow","LOCALS",5)

		SetGlobal("Melf","LOCALS",5)

		SetGlobal("Missile","LOCALS",5)

		Continue()
END

/////////////////////////////////////////////////////////////////////
// Chain Contingency
/////////////////////////////////////////////////////////////////////
IF
    See(NearestEnemyOf(Myself))
    Global("ChainContingency","LOCALS",0)
THEN
  RESPONSE #100
    DisplayString(Myself,26328) // Chain Contingency
    SetGlobal("ChainContingency","LOCALS",1)
    ReallyForceSpell(Myself,WIZARD_SPELL_TRAP) 
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
    ReallyForceSpell(Myself,WIZARD_TIME_STOP)
    SetGlobalTimer("TimeStop","LOCALS",18)
END

/////////////////////////////////////////////////////////////////////
// Wing Buffet
// Spell Range 30, Speed 1, Unlimited 
// This is at the top of the list because we don't ever want to
// get hit (if at all possible). Keep pushing them back. 
/////////////////////////////////////////////////////////////////////
IF
 OR(2)
  ActionListEmpty()
  GlobalTimerExpired("Cast","LOCALS")
 Range(NearestEnemyOf(Myself),20)
THEN
 RESPONSE #100
  ForceSpell(Myself,DRAGON_WING_BUFFET)
  Continue()	// continue after innate actions
END

/////////////////////////////////////////////////////////////////////
// Locate the enemy. 
/////////////////////////////////////////////////////////////////////
IF
	!Detect(NearestEnemyOf(Myself))
	!Detect(Player1)
        !Range(Player1,20)
THEN
	RESPONSE #100
                ForceSpell(Player1,WIZARD_DIMENSION_DOOR)
END

/////////////////////////////////////////////////////////////////////
// Contingency
/////////////////////////////////////////////////////////////////////
IF
    TookDamage()
    Global("Contingency","LOCALS",0)
THEN
    RESPONSE #100
        DisplayString(Myself,40252) // Contingency - Protection ...
        ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
        SetGlobalTimer("RecastProMagWeap","LOCALS",24)
        SetGlobal("Contingency","LOCALS",1)
END

/////////////////////////////////////////////////////////////////////
// Spell: Pro Mag Weapon (if nearby enemy or attacked)
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("RecastProMagWeap","LOCALS")
	GlobalGT("ProMagWeap","LOCALS",0)
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
	Exists(NearestEnemyOf(Myself))
        HPPercentLT(Myself,100)
	OR(2)
	  See(LastAttackerOf(Myself))
	  Range(NearestEnemyOf(Myself),10)
THEN
	RESPONSE #100
	  IncrementGlobal("ProMagWeap","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
          SetGlobalTimer("RecastProMagWeap","LOCALS",24)
	  SpellNoDec(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

/////////////////////////////////////////////////////////////////////
// Spell: Stoneskin (if out)
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Stoneskin","LOCALS",0)
	CheckStatLT(Myself,1,STONESKINS)
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
	  IncrementGlobal("Stoneskin","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	  SpellNoDec(Myself,WIZARD_STONE_SKIN)
END

/////////////////////////////////////////////////////////////////////
// Dragon Breath
/////////////////////////////////////////////////////////////////////
IF
 OR(2)
  ActionListEmpty()
  GlobalTimerExpired("Cast","LOCALS")
 OR(5)
  See(NearestEnemyOfType([0.0.0.BARD_ALL]))
  See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
  See(NearestEnemyOfType([GOODCUTOFF.0.0.0]))
  See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
  See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
 GlobalTimerExpired("YELLOWDRAGONBREATH","GLOBAL")
THEN
 RESPONSE #100
  ForceSpellRES("ritdrag",LastSeenBy(Myself))
  SetGlobalTimer("YELLOWDRAGONBREATH","GLOBAL",18)
  Continue()	// continue after innate actions
END

/////////////////////////////////////////////////////////////////////
// Spell: Time Stop
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("TimeStop","LOCALS")
	GlobalGT("Time","LOCALS",0)
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
	OR(2)
	  See([PC])
	  !CombatCounter(0)
THEN
	RESPONSE #100
	  IncrementGlobal("Time","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	  SpellNoDec(Myself,WIZARD_TIME_STOP)
	  SetGlobalTimer("TimeStop","LOCALS",18)
END

/////////////////////////////////////////////////////////////////////
// Spell: Immunity Abjure
/////////////////////////////////////////////////////////////////////
IF
        GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Abjure","LOCALS",0)
	See([PC])
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
	  IncrementGlobal("Abjure","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
          SpellNoDec(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
END

/////////////////////////////////////////////////////////////////////
// Spell: Spell Shield
/////////////////////////////////////////////////////////////////////
IF
        GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("SpellShield","LOCALS",0)
	See([PC])
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
	  IncrementGlobal("SpellShield","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
          SpellNoDec(Myself,WIZARD_SPELL_SHIELD)
END

/////////////////////////////////////////////////////////////////////
// Spell: Sequencer (mixed)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
        Global("Sequencer","LOCALS",0)
        See([PC])
THEN
    RESPONSE #100
        SetGlobalTimer("Cast","LOCALS",6)
        SetGlobal("Sequencer","LOCALS",1)
        DisplayString(Myself,25951) // Sequencer
        ReallyForceSpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
        ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
        ReallyForceSpell(LastSeenBy(Myself),WIZARD_SLOW)
END

/////////////////////////////////////////////////////////////////////
// Spell: Greater Malison
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Malison","LOCALS",0)
	See([PC])
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
	  IncrementGlobal("Malison","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	SpellNoDec(LastSeenBy(Myself),WIZARD_GREATER_MALISON)
        // dragon innate abilities
        ReallyForceSpell(LastSeenBy(Myself),DRAGON_FEAR)
        ReallyForceSpell(LastSeenBy(Myself),DRAGON_LOWER_FIRE)
END

/////////////////////////////////////////////////////////////////////
// Heal, 50%
/////////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("Cast","LOCALS")
  HasItem("potn55",Myself)
  HPPercentLT(Myself,50)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    UseItem("potn55",Myself)
END

/////////////////////////////////////////////////////////////////////
// Spell: Chaos
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Chaos","LOCALS",0)
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
	See([PC])
THEN
	RESPONSE #100
	  IncrementGlobal("Chaos","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	  SpellNoDec(LastSeenBy(Myself),WIZARD_CHAOS)
END

/////////////////////////////////////////////////////////////////////
// Spell: Wilting
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Wilt","LOCALS",0)
	See([PC])
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
	  IncrementGlobal("Wilt","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

/////////////////////////////////////////////////////////////////////
// Spell: Mordy
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Mordy","LOCALS",0)
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
	OR(2)
	  !CombatCounter(0)
	  See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
	  IncrementGlobal("Mordy","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(Myself,WIZARD_MORDENKAINENS_SWORD)
END

/////////////////////////////////////////////////////////////////////
// Spell: Slow
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Slow","LOCALS",0)
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
	See([PC])
THEN
	RESPONSE #100
	  IncrementGlobal("Slow","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(LastSeenBy(Myself),WIZARD_SLOW)
END

/////////////////////////////////////////////////////////////////////
// Spell: Melf Acid
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("MelfAcid","LOCALS",0)
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
	See([PC])
THEN
	RESPONSE #100
	  IncrementGlobal("MelfAcid","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

/////////////////////////////////////////////////////////////////////
// Spell: Magic Missile
/////////////////////////////////////////////////////////////////////
IF
          GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Missile","LOCALS",0)
        CheckStatLT(Myself,25,SPELLFAILUREMAGE)
	See([PC])
THEN
	RESPONSE #100
	  IncrementGlobal("Missile","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

/////////////////////////////////////////////////////////////////////
// Wail on them!
/////////////////////////////////////////////////////////////////////
IF
   !GlobalTimerExpired("Cast","LOCALS")
   See([PC])
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

/////////////////////////////////////////////////////////////////////
// The Fight Ends (or we're all resting)
// The original resting condition was !Detect([PC]), HPLT(Myself,50)
// CombatCounter(0). Lazy scripting: the HPLT was apparently to guard
// against repeated healings but it meant that a 75% health dragon
// never got back his spell-like abilities. I'm happy to heal
// even when it is useless: it'll make the players edgy. 
/////////////////////////////////////////////////////////////////////
IF
 !Detect(NearestEnemyOf(Myself))	// (*)
 CombatCounter(0)			// (*)
 ActionListEmpty()
 Global("Prep","LOCALS",1)
THEN
 RESPONSE #100
  DisplayString(Myself,26703)		// "Damn, where are they?"
  ForceSpell(Myself,CLERIC_HEAL)
  SetGlobal("Prep","LOCALS",0)
END
