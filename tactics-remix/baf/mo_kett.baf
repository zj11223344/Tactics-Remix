/////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////
//            Improved Smarter Guarded Compound Scripts
/////////////////////////////////////////////////////////////////////
// 				Version 1
// Ketta, 19th level Thief
// 
// Fixes: Ketta no longer tries to backstab with her fist (sigh).
//
// Improvements: In order to get the most bang out of her 10 
// backstabs, Sion will double-cast Strength on her (to get her
// up to 18/00). She will also avoid backstabbing creatures with
// Stoneskins. She won't go downstairs. 
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Battle Prep
// 
// The GC crew knows you are invading, so they prepare by casting
// a few long-lived spells while waiting for the demon and efreeti 
// to take you out. 
/////////////////////////////////////////////////////////////////////
IF
  Detect(NearestEnemyOf(Myself))
  Global("Prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    GiveItemCreate("potn52",Myself,5,0,0)   // extra healing
    EquipMostDamagingMelee()
    Hide()
END

IF
  Detect(NearestEnemyOf(Myself))
  HasItem("potn06",Myself)
THEN
  RESPONSE #100
    UseItem("potn06",Myself)
END

/////////////////////////////////////////////////////////////////////
// Shadowstep - When low on health, shadowstep to run away
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  !GlobalTimerNotExpired("BD_SSTEP","LOCALS")
  HaveSpellRES("SPSD02")  // Shadowstep
  HPPercentLT(Myself,50)
  Range(NearestEnemyOf(Myself),6)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetGlobalTimer("BD_SSTEP","LOCALS",7)
    SetInterrupt(FALSE)
    SpellRES("SPSD02",Myself)  // Shadowstep
    SetInterrupt(TRUE)
END

IF
  GlobalTimerNotExpired("BD_SSTEP","LOCALS")
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    RunAwayFrom(NearestEnemyOf(Myself),15) // One second running away before re-evaluate
END

/////////////////////////////////////////////////////////////////////
// Heal!
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  HasItem("potn52",Myself)
  HPPercentLT(Myself,50)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetInterrupt(FALSE)
    UseItem("potn52",Myself)
    SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////////
// Backstab Attacks 
/////////////////////////////////////////////////////////////////////
IF
  ActionListEmpty()
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  See([GOODCUTOFF.0.0.MAGE_ALL])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  See([GOODCUTOFF.0.0.DRUID_ALL])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  See([GOODCUTOFF.0.0.CLERIC_ALL])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  See([GOODCUTOFF.0.0.THIEF_ALL])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  See([GOODCUTOFF.0.0.BARD_ALL])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  See([GOODCUTOFF.0.0.RANGER_ALL])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  See([GOODCUTOFF.0.0.PALADIN_ALL])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  See([GOODCUTOFF.0.0.FIGHTER_ALL])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

/////////////////////////////////////////////////////////////////////
// Run and hide in plain sight!
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  //!HasItemSlot(Myself,SLOT_MISC)
  !ButtonDisabled(BUTTON_STEALTH)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(NearestEnemyOf(Myself),3)
    DisplayStringHead(Myself,101647)  // *attempts to hide in shadows*
    Hide()
    SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////////
// Drink!
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  HasItem("potn10",Myself)
  ButtonDisabled(BUTTON_STEALTH)
THEN
  RESPONSE #100
    RunAwayFrom(NearestEnemyOf(Myself),3)
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetInterrupt(FALSE)
    UseItem("potn10",Myself)
    SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////////
// Attack
/////////////////////////////////////////////////////////////////////
IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SecondNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SecondNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(ThirdNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(ThirdNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FourthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FourthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FifthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FifthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SixthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SecondNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SecondNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(ThirdNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(ThirdNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FourthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FourthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FifthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FifthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SixthNearestEnemyOf(Myself))
END

/////////////////////////////////////////////////////////////////////
// Find them!
/////////////////////////////////////////////////////////////////////
IF
  Heard([ENEMY],125)
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    MoveToObject(LastHeardBy(Myself))
END
