// Tactics Remix, Improved Guarded Compound in the Temple District

///////////////////////////////////////////////////////////////////////////
// Adds Action to Use a Specific Item Ability                            //
///////////////////////////////////////////////////////////////////////////
APPEND ~action.ids~ ~34 UseItemAbility(S:Object*,O:Target*,I:Slot*SLOTS,I:Ability*)~ UNLESS ~34 UseItemAbility(S:Object*,O:Target*,I:Slot*SLOTS,I:Ability*)~

///////////////////////////////////////////////////////////////////////////
// Update Maferan                                                        //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~hlmafer.CRE~ ~override~
  WRITE_ASCII 0x250 ~~        #8 // Clear Class Script
  WRITE_ASCII 0x258 ~~        #8 // Clear Race Script
  WRITE_ASCII 0x260 ~~        #8 // Clear General Script
  WRITE_ASCII 0x268 ~mo_thug~ #8 // Add Default Script

  PATCH_IF !scs_ai BEGIN
    WRITE_BYTE 0x52 4 // thac0
    SET_BG2_PROFICIENCY ~PROFICIENCYAXE~ 5
    SET_BG2_PROFICIENCY ~PROFICIENCYSWORDANDSHIELD~ 2
  END

///////////////////////////////////////////////////////////////////////////
// Update Koshi                                                          //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~hlkoshi.CRE~ ~override~
  WRITE_ASCII 0x250 ~~        #8 // Clear Class Script
  WRITE_ASCII 0x258 ~~        #8 // Clear Race Script
  WRITE_ASCII 0x260 ~~        #8 // Clear General Script
  WRITE_ASCII 0x268 ~mo_kosh~ #8 // Add Default Script

  PATCH_IF !scs_ai BEGIN
    LPF ADD_CRE_EFFECT INT_VAR opcode = 0 target = 2 parameter1 = 2 parameter2 = 0 timing = 9 resist_dispel = 0 probability1 = 100 END
    LPF ADD_CRE_EFFECT INT_VAR opcode = 73 target = 1 parameter1 = 5 parameter2 = 0 timing = 9 resist_dispel = 0 probability1 = 100 END
    LPF ADD_CRE_EFFECT INT_VAR opcode = 190 target = 1 parameter1 = 4 timing = 9 resist_dispel = 0 probability1 = 100 END
    LPF ADD_CRE_EFFECT INT_VAR opcode = 278 target = 1 parameter1 = 5 parameter2 = 0 timing = 9 resist_dispel = 0 probability1 = 100 END
    SET_BG2_PROFICIENCY ~PROFICIENCYKATANA~ 5
    SET_BG2_PROFICIENCY ~PROFICIENCY2WEAPON~ 3
  END

///////////////////////////////////////////////////////////////////////////
// Update Olaf                                                           //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~hlolaf.CRE~ ~override~
  WRITE_ASCII 0x250 ~~        #8 // Clear Class Script
  WRITE_ASCII 0x258 ~~        #8 // Clear Race Script
  WRITE_ASCII 0x260 ~~        #8 // Clear General Script
  WRITE_ASCII 0x268 ~mo_thug~ #8 // Add Default Script

  PATCH_IF !scs_ai BEGIN
    WRITE_BYTE 0x52 4 // thac0
    SET_BG2_PROFICIENCY ~PROFICIENCYTWOHANDEDSWORD~ 5
    SET_BG2_PROFICIENCY ~PROFICIENCY2HANDED~ 2
  END

///////////////////////////////////////////////////////////////////////////
// Update Scion                                                          //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~hlsion.CRE~ ~override~
  WRITE_ASCII 0x250 ~hlsion~  #8 // Add Class Script
  WRITE_ASCII 0x258 ~~        #8 // Clear Race Script
  WRITE_ASCII 0x260 ~~        #8 // Clear General Script
  WRITE_ASCII 0x268 ~mo_sion~ #8 // Add Default Script

///////////////////////////////////////////////////////////////////////////
// Update Stalman                                                        //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~hlstal.CRE~ ~override~
  WRITE_ASCII 0x250 ~~        #8 // Clear Class Script
  WRITE_ASCII 0x258 ~~        #8 // Clear Race Script
  WRITE_ASCII 0x260 ~~        #8 // Clear General Script
  WRITE_ASCII 0x268 ~mo_stal~ #8 // Add Default Script

///////////////////////////////////////////////////////////////////////////
// Update Ketta                                                          //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~hlketta.CRE~ ~override~
  WRITE_SHORT 0x246 0x4021       // Shadowdancer
  WRITE_ASCII 0x250 ~hlketta~ #8 // Add Class Script
  WRITE_ASCII 0x258 ~~        #8 // Clear Race Script
  WRITE_ASCII 0x260 ~~        #8 // Clear General Script
  WRITE_ASCII 0x268 ~mo_kett~ #8 // Add Default Script

///////////////////////////////////////////////////////////////////////////
// Copy .BCS file to override folder                                     //
///////////////////////////////////////////////////////////////////////////
COPY ~tactics-remix/baf/guarded_compound/mo_thug.baf~ ~override/mo_thug.bcs~
     ~tactics-remix/baf/guarded_compound/mo_kosh.baf~ ~override/mo_kosh.bcs~
     ~tactics-remix/baf/guarded_compound/mo_kett.baf~ ~override/mo_kett.bcs~
     ~tactics-remix/baf/guarded_compound/mo_stal.baf~ ~override/mo_stal.bcs~
     ~tactics-remix/baf/guarded_compound/hlsion.baf~  ~override/hlsion.bcs~
     ~tactics-remix/baf/guarded_compound/hlketta.baf~ ~override/hlketta.bcs~
  COMPILE_BAF_TO_BCS

ACTION_IF !scs_ai BEGIN // No SCS
  COPY ~tactics-remix/baf/guarded_compound/ar0907.baf~   ~override/ar0907.bcs~
       ~tactics-remix/baf/guarded_compound/mo_sion1.baf~ ~override/mo_sion.bcs~
    COMPILE_BAF_TO_BCS
END ELSE BEGIN // SCS
  COPY ~tactics-remix/baf/guarded_compound/mo_sion2.baf~ ~override/mo_sion.bcs~
    COMPILE_BAF_TO_BCS
END