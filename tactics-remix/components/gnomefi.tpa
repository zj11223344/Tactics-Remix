// Tactics Remix, Kensai Ryu's Gnome Fighter/Illusionist

///////////////////////////////////////////////////////////////////////////
// Import Files                                                          //
///////////////////////////////////////////////////////////////////////////
// Littlefang
COPY ~tactics-remix/itm/MO1H11.ITM~ ~override~
  SAY NAME1 @4000
  SAY NAME2 @4002
  SAY UNIDENTIFIED_DESC @4001
  SAY DESC @4003
  WRITE_ASCII 0x58 csw1h09
  WRITE_LONG 0x34 4000

// The Lucky One
COPY ~tactics-remix/itm/MO1H12.ITM~ ~override~
  SAY NAME1 @4000
  SAY NAME2 @4004
  SAY UNIDENTIFIED_DESC @4001
  SAY DESC @4005
  WRITE_ASCII 0x58 csw1h07
  WRITE_LONG 0x34 1000

COPY ~tactics-remix/cre/mofam01.cre~ ~override~
  SAY NAME1 @2001
  SAY NAME2 @2001
COPY ~tactics-remix/cre/mofam02.cre~ ~override~
  SAY NAME1 @2002
  SAY NAME2 @2002
COPY ~tactics-remix/cre/mognome.cre~ ~override~
  SAY NAME1 @2000
  SAY NAME2 @2000
  REPLACE_CRE_ITEM ~MO1H11~ #0 #0 #0 ~UNSTEALABLE~ ~WEAPON1~ EQUIP
  REPLACE_CRE_ITEM ~MO1H12~ #0 #0 #0 ~UNSTEALABLE~ ~SHIELD~ EQUIP
  REPLACE_CRE_ITEM ~CHAN13~ #0 #0 #0 ~UNSTEALABLE~ ~ARMOR~ EQUIP
  REPLACE_CRE_ITEM ~HELM01~ #0 #0 #0 ~UNSTEALABLE~ ~HELMET~ EQUIP
  SET_BG2_PROFICIENCY ~PROFICIENCY2WEAPON~ 3
  SET_BG2_PROFICIENCY ~PROFICIENCYSHORTSWORD~ 5
COPY ~tactics-remix/dlg/mognome.dlg~ ~override~

///////////////////////////////////////////////////////////////////////////
// Create and Add Spell State to Track Prior Target                      //
///////////////////////////////////////////////////////////////////////////
ACTION_IF !target_tracking BEGIN
  OUTER_SET target_tracking=1
  ACTION_IF (FILE_CONTAINS_EVALUATED (~splstate.ids~ ~PRIOR_TARGET~)) BEGIN
    COPY_EXISTING ~splstate.ids~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 1 ~state_name~
      READ_2DA_ENTRY_FORMER rows row 0 ~state_ind~
      PATCH_IF (~%state_name%~ STRING_EQUAL_CASE ~PRIOR_TARGET~) BEGIN
      SET pr_trgt = %state_ind%
      END
    END
    BUT_ONLY
  END

  ACTION_IF !(VARIABLE_IS_SET %pr_trgt%) BEGIN
    LAF ds_sort_ids STR_VAR ids=splstate END
    LAF d5_resolve_state STR_VAR new_state_id = ~PRIOR_TARGET~ RET new_state_ind END
    OUTER_SET pr_trgt = %new_state_ind%
  END

  COPY ~tactics-remix/spl/motrgt.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 2 special = 1 parameter2 = %pr_trgt% timing = 0 duration = 10 END
END

///////////////////////////////////////////////////////////////////////////
// Update AI Scripts Based on Installed Mods                             //
///////////////////////////////////////////////////////////////////////////
ACTION_IF iwdification BEGIN
  INCLUDE ~tactics-remix/lib/iwd_dispelling.tph~
  INCLUDE ~tactics-remix/lib/iwd_dispelling_f1.tph~
END

///////////////////////////////////////////////////////////////////////////
// Copy .BCS file to override folder                                     //
///////////////////////////////////////////////////////////////////////////
EXTEND_TOP ~AR0326.bcs~ ~tactics-remix/baf/gnomefi/ar0326.baf~

COPY ~tactics-remix/baf/gnomefi/mofam01.baf~ ~override/mofam01.bcs~
     ~tactics-remix/baf/gnomefi/mofam02.baf~ ~override/mofam02.bcs~
     ~tactics-remix/baf/gnomefi/mognome.baf~ ~override/mognome.bcs~
  COMPILE_BAF_TO_BCS

