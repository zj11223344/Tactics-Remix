// Tactics Remix, Kuroisan

///////////////////////////////////////////////////////////////////////////
// Import Files                                                          //
///////////////////////////////////////////////////////////////////////////
COPY ~tactics-remix/spl/solak1.spl~  ~override~ 
        SAY NAME1 @4104 
        SAY NAME2 @4104
        PATCH_FOR_EACH fireshield IN
          balshld2
          bdbarbde
          bdsha12a
          bdshld02
          keldorn
          leat23a
          spcl237d
          spimixd
          sppr730d
          sppr951d
          sppr952d
          spwi403d
          spwi418d
          mobtr22
        BEGIN
          PATCH_IF FILE_EXISTS_IN_GAME ~%fireshield%.SPL~ THEN BEGIN
            LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 timing = 10 resist_dispel = 0 duration = 2 probability1 = 100 STR_VAR resource = ~%fireshield%~ END
          END
        END
        
COPY ~tactics-remix/itm/solak1b.itm~ ~override~
COPY ~tactics-remix/itm/solak1.itm~  ~override~
        SAY NAME1 @4100
        SAY NAME2 @4102
        SAY UNIDENTIFIED_DESC @4101
        SAY DESC @4103
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 232 target = 1 parameter1 = 1 parameter2 = 0 timing = 2 resist_dispel = 1 probability1 = 100 STR_VAR resource = ~solak1~ END

COPY ~tactics-remix/eff/morb03.eff~  ~override~
COPY ~tactics-remix/itm/morb03.itm~  ~override~
        SAY NAME1 @4200
        SAY NAME2 @4202
        SAY UNIDENTIFIED_DESC @4201
        SAY DESC @4203
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 STR_VAR resource = ~morb03~ END

COPY ~tactics-remix/itm/mobt09.itm~  ~override~
        SAY NAME1 @4300
        SAY NAME2 @4302
        SAY UNIDENTIFIED_DESC @4301
        SAY DESC @4303
        PATCH_IF FILE_EXISTS_IN_GAME ~#curefer.spl~ BEGIN // fear cure from EEFP
          LPF ALTER_EFFECT INT_VAR match_opcode = 161 opcode = 146 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~#curefer~ END // instead of curing fear, cast fear removal spell
          LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 36 END // delete removal of fear icon (handled by spell)
        END 

COPY ~tactics-remix/cre/solak1.cre~ ~override~
  SAY NAME1 @2003
  SAY NAME2 @2003
  REPLACE_CRE_ITEM ~morb03~ #0 #0 #0 ~UNSTEALABLE~ ~ARMOR~ EQUIP
  REPLACE_CRE_ITEM ~mobt09~ #0 #0 #0 ~UNSTEALABLE~ ~BOOTS~ EQUIP

COMPILE EVALUATE_BUFFER ~tactics-remix/dlg/solak1.d~

///////////////////////////////////////////////////////////////////////////
// Create and Add Spell State to Track Prior Target                      //
///////////////////////////////////////////////////////////////////////////
ACTION_IF !target_tracking BEGIN
  OUTER_SET target_tracking=1
  ACTION_IF (FILE_CONTAINS_EVALUATED (~splstate.ids~ ~PRIOR_TARGET~)) BEGIN
    COPY_EXISTING ~splstate.ids~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 1 ~state_name~
      READ_2DA_ENTRY_FORMER rows row 0 ~state_ind~
      PATCH_IF (~%state_name%~ STRING_EQUAL_CASE ~PRIOR_TARGET~) BEGIN
      SET pr_trgt = %state_ind%
      END
    END
    BUT_ONLY
  END

  ACTION_IF !(VARIABLE_IS_SET %pr_trgt%) BEGIN
    LAF ds_sort_ids STR_VAR ids=splstate END
    LAF d5_resolve_state STR_VAR new_state_id = ~PRIOR_TARGET~ RET new_state_ind END
    OUTER_SET pr_trgt = %new_state_ind%
  END

  COPY ~tactics-remix/spl/motrgt.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 2 special = 1 parameter2 = %pr_trgt% timing = 0 duration = 10 END
END

///////////////////////////////////////////////////////////////////////////
// Update AI Scripts Based on Installed Mods                             //
///////////////////////////////////////////////////////////////////////////
ACTION_IF iwdification BEGIN
  INCLUDE ~tactics-remix/lib/iwd_dispelling_ak.tph~
END

///////////////////////////////////////////////////////////////////////////
// Copy .BCS file to override folder                                     //
///////////////////////////////////////////////////////////////////////////
ACTION_IF GAME_IS ~eet~ BEGIN
  EXTEND_TOP ~baldur.bcs~   ~tactics-remix/baf/kuroisan/solak1mo.baf~
  EXTEND_TOP ~baldur25.bcs~ ~tactics-remix/baf/kuroisan/solak1mo.baf~
END ELSE ACTION_IF GAME_IS ~bg2ee~ BEGIN
  EXTEND_TOP ~baldur.bcs~   ~tactics-remix/baf/kuroisan/solak1me.baf~
  EXTEND_TOP ~baldur25.bcs~ ~tactics-remix/baf/kuroisan/solak1me.baf~
END

COPY ~tactics-remix/baf/solak1.baf~ ~override/solak1.bcs~
  COMPILE_BAF_TO_BCS

